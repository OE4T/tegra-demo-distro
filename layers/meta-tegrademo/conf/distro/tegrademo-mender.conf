include tegrademo.conf
REQUIRED_TD_BBLAYERS_CONF_VERSION = "${DISTRO}-8"

DISTRO = "tegrademo-mender"
DISTRO_NAME = "OE4Tegra Demonstration Distro with Mender"

# The distro version doesn't include DATE to avoid basehash changes in do_image_mender
DISTRO_VERSION = "${DISTRO_VERSION_BASE}+snapshot"

### Additions for mender ###

INHERIT += "mender-full distro_layer_buildinfo"
INHERIT:append:tegra = " tegra-mender-setup mender-workarounds"
DISTRO_LAYER = "meta-tegrademo"

BUILDNAME ?= "${@d.getVar('DISTRO_VERSION').replace('+snapshot','')}${DISTRO_LAYER_BUILDTAG}"
BUILDNAME[vardepvalue] = "${DISTRO_VERSION}"

MENDER_ARTIFACT_NAME ?= "${IMAGE_BASENAME}_${BUILDNAME}"

IMAGE_FSTYPES:tegra = "tegraflash mender dataimg"
IMAGE_FSTYPES:pn-tegra-minimal-initramfs:tegra = "${INITRAMFS_FSTYPES}"
IMAGE_FSTYPES:pn-tegra-initrd-flash-initramfs:tegra = "${TEGRA_INITRD_FLASH_INITRAMFS_FSTYPES}"
IMAGE_FEATURES:append = " read-only-rootfs"
IMGCLASSES:append = " rootfs-postcommands-overrides"

# Make more room for the data partition, so we can use it to hold
# docker container filesystems.
MENDER_DATA_PART_SIZE_MB_DEFAULT:tegra = "3072"

# Account for the additional 950MB we used to reserve via a separate
# variable, on top of the default 1024MB.  On Nano-2gb, it was 5046MB
# to cover the addition of the swap partition.
TEGRA_MENDER_RESERVED_SPACE_MB = "1974"
TEGRA_MENDER_RESERVED_SPACE_MB:jetson-nano-2gb-devkit = "6070"

# Mender machines need to run checks after an update
SYSTEMD_DEFAULT_TARGET = "finished-booting.target"

# Use cboot by default on TX2 to avoid uboot slot alignment issues
PREFERRED_PROVIDER_virtual/bootloader:tegra186 = "cboot-t18x"

# Use u-boot by default on the TX2 COT when using the FIT image
PREFERRED_PROVIDER_virtual/bootloader:jetson-tx2-devkit-cot = "u-boot-tegra"
