include tegrademo.conf
REQUIRED_TD_BBLAYERS_CONF_VERSION = "${DISTRO}-6"

DISTRO = "tegrademo-mender"
DISTRO_NAME = "OE4Tegra Demonstration Distro with Mender"

# The distro version doesn't include DATE to avoid basehash changes in do_image_mender
DISTRO_VERSION = "${DISTRO_VERSION_BASE}+snapshot"

### Additions for mender ###

INHERIT += "mender-full distro_layer_buildinfo"
INHERIT:append:tegra = " tegra-mender-setup mender-workarounds"
DISTRO_LAYER = "meta-tegrademo"

BUILDNAME ?= "${@d.getVar('DISTRO_VERSION').replace('+snapshot','')}${DISTRO_LAYER_BUILDTAG}"
BUILDNAME[vardepvalue] = "${DISTRO_VERSION}"

MENDER_ARTIFACT_NAME ?= "${IMAGE_BASENAME}_${BUILDNAME}"

IMAGE_FSTYPES:tegra = "tegraflash mender dataimg"
IMAGE_FSTYPES:pn-tegra-minimal-initramfs:tegra = "${INITRAMFS_FSTYPES}"
IMAGE_FEATURES:append = " read-only-rootfs"
IMGCLASSES:append = " rootfs-postcommands-overrides"

# Reserved space for tegraflash partitions, not including APP and DATA
TEGRA_FLASH_PARTITIONS_MB ?= "950"

# Make more room for the data partition, so we can use it to hold
# docker container filesystems.
MENDER_DATA_PART_SIZE_MB_DEFAULT:tegra = "3072"

MENDER_STORAGE_DEVICE_BASE ?= "/dev/mmcblk0p"
MENDER_STORAGE_TOTAL_SIZE_MB ?= "${@tegra_mender_calc_total_size(d) - int(d.getVar('TEGRA_FLASH_PARTITIONS_MB'))}"

# Mender machines need to run checks after an update
SYSTEMD_DEFAULT_TARGET = "finished-booting.target"

IMAGE_INSTALL:append:tegra = " tegra-eeprom-tool i2c-tools tegra-bup-payload"

# 5046 = 950 (default from above) plus 4096 for the swap partition
TEGRA_FLASH_PARTITIONS_MB:jetson-nano-2gb-devkit = "5046"
MENDER_DATA_PART_NUMBER:jetson-nano-devkit = "3"
MENDER_ROOTFS_PART_B_NUMBER:jetson-nano-devkit = "2"
MENDER_DATA_PART_NUMBER:jetson-nano-devkit-emmc = "19"
MENDER_ROOTFS_PART_B_NUMBER:jetson-nano-devkit-emmc = "18"
# Swap is in partition 3 on the 2GB devkit
MENDER_DATA_PART_NUMBER:jetson-nano-2gb-devkit = "4"
MENDER_ROOTFS_PART_B_NUMBER:jetson-nano-2gb-devkit = "2"
# Overrides until meta-mender-tegra is updated
BOOTENV_SIZE:jetson-nano-devkit = "0x8000"
BOOTENV_SIZE:jetson-nano-2gb-devkit = "0x8000"

# Use cboot by default on TX2 to avoid uboot slot alignment issues
PREFERRED_PROVIDER_virtual/bootloader:tegra186 = "cboot-t18x"

# Use u-boot by default on the TX2 COT when using the FIT image
PREFERRED_PROVIDER_virtual/bootloader:jetson-tx2-devkit-cot = "u-boot-tegra"
